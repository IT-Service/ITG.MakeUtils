# $1 - source .sfd file name
# $2 - destination .ttf file name
# $3 - True Type font version string

Open($1)
Reencode("unicode")

# add 0-9, +-=() subscript
subscriptScale = 100*10/14
Select(0u30, 0u39) # 0-9
CopyReference()
Select(0u2080, 0u2089)
Paste()

Select(0u2B) # +
CopyReference()
Select(0u208A)
Paste()
Select(0u2212) # -
CopyReference()
Select(0u208B)
Paste()
Select(0u3D) # =
CopyReference()
Select(0u208C)
Paste()
Select(0u28, 0u29) # ()
CopyReference()
Select(0u208D, 0u208E)
Paste()

Select(0u2080, 0u208E) # 0-9 +-=()
Scale(subscriptScale, 0, 0)
Move(0, -500)

# add 0-9, +-=() superscript
Select(0u2080) # 0
CopyReference()
Select(0u2070)
Paste()
Select(0u2081) # 1
CopyReference()
Select(0u00B9)
Paste()
Select(0u2082, 0u2083) # 2-3
CopyReference()
Select(0u00B2, 0u00B3)
Paste()
Select(0u2084, 0u208E) # 4-9, +-=()
CopyReference()
Select(0u2074, 0u207E)
Paste()

SelectSingletons(0u2070, 0u00B9) # 0, 1
SelectMore(0u00B2, 0u00B3, 0u2074, 0u207E) # 2-3, 4-9, +-=()
Move(0, 1400)

# convert stroked font from Back layer to outline font in Fore layer
SelectNone()
SelectSingletons("exclam", "question", "numbersign", "ampersand", "quotesingle", "AE", "multiply", "slash", "uni26AC", "parenleft", "hyphen", "period", "asterisk", "plus", "comma", "dollar")
SelectMore("zero", "nine")
SelectMoreSingletons("less", "backslash", "bar", "braceleft", "bracketleft", "asciicircum", "asciitilde")
SelectMoreSingletons("underscore", "grave", "at", "ring")
SelectMoreSingletons("uni0335")
SelectMore("A", "Z")
SelectMore("a", "z")
SelectMore("Alpha", "uni03A9")
SelectMore("alpha", "omega")
SelectMore("exclamdown", "ordmasculine")
SelectMore("guilsinglleft", "guilsinglleft")
SelectMoreSingletons("dotlessi", "uni0237")
SelectMoreSingletons("Thorn", "thorn", "eth")
SelectMoreSingletons("Lslash", "lslash")
SelectMore("uni0304", "uni0304")
SelectMoreSingletons("uni0308", "uni030A")
SelectMore("uni0326", "uni0328")
SelectMore("uni0336", "uni0338")
SelectMore("uni0401", "uni042F") # А-Я
SelectMore("uni0430", "uni044F") # а-я
SelectMore(0u2010, 0u201F)
SelectMore(0u2021, 0u2049)
SelectMoreSingletons(0u20AC, 0u20BD)
SelectMore(0u2103, 0u2122)
SelectMore(0u2200, 0u2219)
SelectMoreSingletons("uni20DD", "uni2E32")
SelectMoreSingletons("ae")
SelectMore("uni1D3F", "uni1D3F") # A-Z superscript
ExpandStroke(100, 1, 1)
RemoveOverlap()
Simplify()
#CorrectDirection()

# set font version
SetFontNames('','','','','',$3)
SetTTFName(1033, 5, $3)
SetTTFName(1049, 5, $3)

# build accented glyphs
SelectWorthOutputting()
SelectInvert()
BuildAccented()

# scale to new em size
ScaleToEm(1024)

# generate font files
Generate($2)
