# $1 - source .sfd file name
# $2 - destination .ttf file name
# $3 - True Type font version string

Open($1)

# add 0-9 superscript
Select(0u30) # 0
CopyReference()
Select(0u2070)
Paste()
Select(0u31) # 1
CopyReference()
Select(0u00B9)
Paste()
Select(0u32, 0u33) # 2-3
CopyReference()
Select(0u00B2, 0u00B3)
Paste()
Select(0u34, 0u39) # 4-9
CopyReference()
Select(0u2074, 0u2079)
Paste()

SelectNone()
SelectSingletons(0u2070, 0u00B9) # 0, 1
SelectMore(0u00B2, 0u00B3, 0u2074, 0u2079) # 2-3, 4-9
#BuildComposite()
Scale(100*10/14, 0, 0)
Move(0, 900)

# convert stroked font from Back layer to outline font in Fore layer
SelectNone()
SelectSingletons("exclam", "question", "numbersign", "ampersand", "quotesingle", "AE", "multiply", "slash", "uni26AC", "parenleft", "hyphen", "period", "asterisk", "plus", "comma", "dollar")
SelectMore("zero", "nine")
SelectMoreSingletons("less", "backslash", "bar", "braceleft", "bracketleft", "asciicircum", "asciitilde")
SelectMoreSingletons("underscore", "grave", "at")
SelectMoreSingletons("uni0335")
SelectMore("A", "Z")
SelectMore("a", "z")
SelectMore("exclamdown", "ordmasculine")
SelectMore("guilsinglleft", "guilsinglleft")
SelectMoreSingletons("dotlessi", "uni0237")
SelectMoreSingletons("Thorn", "thorn", "eth")
SelectMore("uni0304", "uni0304")
SelectMoreSingletons("uni0308")
SelectMore("uni0326", "uni0328")
SelectMore("uni0337", "uni0338")
SelectMoreSingletons("uni20DD", "uni2E32")
SelectMoreSingletons("ae")
SelectMore("uni1D3F", "uni1D3F") # A-Z superscript
ExpandStroke(100, 1, 1)
RemoveOverlap()
Simplify()
#CorrectDirection()

# set font version
SetFontNames('','','','','',$3)
SetTTFName(1033, 5, $3)
SetTTFName(1049, 5, $3)

# build accented glyphs
Reencode("unicode")
SelectWorthOutputting()
SelectInvert()
BuildAccented()

# scale to new em size
ScaleToEm(1024)

# generate font files
Generate($2)
